@Library('hibernate-jenkins-pipeline-helpers') _

import org.hibernate.jenkins.pipeline.helpers.version.Version

pipeline {
    agent {
        label 'Release'
    }
    tools {
        jdk 'OpenJDK 21 Latest'
    }
    options {
        buildDiscarder logRotator(daysToKeepStr: '30', numToKeepStr: '10')
        disableConcurrentBuilds(abortPrevious: false)
    }
    parameters {
        string(
                name: 'RELEASE_VERSION',
                defaultValue: '',
                description: 'The version to be released, e.g. 1.1.0.',
                trim: true
        )
        string(
                name: 'DEVELOPMENT_VERSION',
                defaultValue: '',
                description: 'The next version to be used after the release, e.g. 1.2.0-SNAPSHOT.',
                trim: true
        )
        booleanParam(
                name: 'RELEASE_DRY_RUN',
                defaultValue: false,
                description: 'If true, just simulate the release, without pushing any commits or tags, and without uploading any artifacts or documentation.'
        )
    }
    stages {
        stage('Release') {
            steps {
                script {
                    // Check that all the necessary parameters are set
                    if (!params.RELEASE_VERSION) {
                        throw new IllegalArgumentException("Missing value for parameter RELEASE_VERSION.")
                    }
                    if (!params.DEVELOPMENT_VERSION) {
                        throw new IllegalArgumentException("Missing value for parameter DEVELOPMENT_VERSION.")
                    }

                    def releaseVersion = Version.parseReleaseVersion(params.RELEASE_VERSION, Version.Scheme.JBOSS_NO_FINAL)
                    def developmentVersion = Version.parseDevelopmentVersion(params.DEVELOPMENT_VERSION)
                    echo "Performing full release for version ${releaseVersion.toString()}"

                    configFileProvider([configFile(fileId: 'release.config.ssh', targetLocation: env.HOME + '/.ssh/config'),
                                        configFile(fileId: 'release.config.ssh.knownhosts', targetLocation: env.HOME + '/.ssh/known_hosts')]) {
                        // using MAVEN_GPG_PASSPHRASE (the default env variable name for passphrase in maven gpg plugin)
                        withCredentials([file(credentialsId: 'release.gpg.private-key', variable: 'RELEASE_GPG_PRIVATE_KEY_PATH'),
                                         string(credentialsId: 'release.gpg.passphrase', variable: 'JRELEASER_GPG_PASSPHRASE'),
                                         usernamePassword(credentialsId: 'ossrh.sonatype.org', passwordVariable: 'JRELEASER_NEXUS2_PASSWORD', usernameVariable: 'JRELEASER_NEXUS2_USERNAME'),
                                         string(credentialsId: 'Hibernate-CI.github.com', variable: 'JRELEASER_GITHUB_TOKEN')]) {
                            sshagent(['ed25519.Hibernate-CI.github.com']) {
                                sh 'cat $HOME/.ssh/config'
                                sh 'git clone https://github.com/hibernate/hibernate-release-scripts.git'
                                env.RELEASE_GPG_HOMEDIR = env.WORKSPACE_TMP + '/.gpg'
                                sh """
                                    bash -xe hibernate-release-scripts/release.sh ${params.RELEASE_DRY_RUN ? '-d' : ''} -b ${env.BRANCH_NAME} \
                                            models ${releaseVersion.toString()} ${developmentVersion.toString()}
                                """
                            }
                        }
                    }
                }
            }
        }
    }
}
