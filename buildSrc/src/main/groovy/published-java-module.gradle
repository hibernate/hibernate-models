plugins {
	id "java-module"
	id "testing"
	id "code-quality"

	id "maven-publish"
	id "publishing-config"
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Java handling
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

java {
	withJavadocJar()
	withSourcesJar()
}

def moduleNameBase = project.name.startsWith( 'hibernate-' ) ? name.drop( 'hibernate-'.length() ): name
def moduleName = "org.hibernate.$moduleNameBase".replace('-','.')

tasks.named("jar") {
	manifest {
		attributes(
				'Automatic-Module-Name': moduleName
		)
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Javadoc
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

tasks.named( "javadoc", Javadoc ) {
	options {
		use = true
		encoding = "UTF-8"

		addStringOption( "Xdoclint:none", "-quiet" )

		tags(
				"todo:X",
				"apiNote:a:API Note:",
				"implSpec:a:Implementation Specification:",
				"implNote:a:Implementation Note:"
		)
	}
}
