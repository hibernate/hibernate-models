import java.nio.charset.StandardCharsets

plugins {
	id "base-information"
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Release processing
//
// Processes should execute the following tasks in order
//		- prepareForRelease 				(this script)
//		- publishToSonatype					(io.github.gradle-nexus.publish-plugin)
//		- closeSonatypeStagingRepository	(io.github.gradle-nexus.publish-plugin)
//		- completeRelease 					(this script)
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

def releaseVersion = project.ext.releaseVersion as String
def developmentVersion = project.ext.developmentVersion as String


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Release processing - preparation
//		- releasePreparation (task)
//			- check for clean working copy
//			- clean & compile
//		- changeToReleaseVersion (task)
//			- change version in `/version.txt` to the release version
//			- commit the version change
//
// Processes should execute `prepareForRelease`
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

def releasePreparationTask = tasks.register( "releasePreparation" ) {
	dependsOn tasks.named( "clean" )
	dependsOn tasks.named( "compile" )

	doFirst {
		logger.lifecycle( "Checking that all commits are pushed..." )
//		String diffWithUpstream = executeGitCommand( 'diff', '@{u}' )
//		if ( !diffWithUpstream.isEmpty() ) {
//			throw new GradleException(
//					"Cannot perform `prepareForRelease` tasks because there are un-pushed local commits .\n" +
//							"Push your commits first."
//			);
//		}
	}
}

def changeToReleaseVersionTask = tasks.register( "changeToReleaseVersion" ) {
	group 'Release'
	description 'Updates `version.txt` file to the specified release-version'

	dependsOn releasePreparationTask
	onlyIf {
		releasePreparationTask.get().didWork
	}

	doFirst {
		logger.lifecycle( "Updating version-file to release-version : `${releaseVersion}`" )
		updateVersionFile( releaseVersion )
	}

	doLast {
		logger.lifecycle( "Performing pre-steps Git commit : `${releaseVersion}`" )
		executeGitCommand( 'add', '.' )
		executeGitCommand( 'commit', '-m', "Pre-steps for release : `${releaseVersion}`" )
	}
}

tasks.register( "prepareForRelease" ) {
	dependsOn releasePreparationTask
	dependsOn changeToReleaseVersionTask
}


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Publish
//
// Processes should execute
// 		- `publishToSonatype`
// 		- `closeSonatypeStagingRepository`
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Release processing - complete
//		- tagRelease (task)
//			- tag
//		- changeToDevelopmentVersion (task)
//			- change version in `/version.txt` to the dev version
//			- commit the version change
//
//
// Processes should execute `completeRelease`
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

def tagReleaseTask = tasks.register( "tagRelease" ) {
	onlyIf {
		changeToReleaseVersionTask.get().didWork
	}

	doLast {
		logger.lifecycle( "Tagging release : `${releaseVersion}`..." )
		executeGitCommand( 'tag', '-a', releaseVersion, '-m', "Release ${releaseVersion}" )
	}
}

def changeToDevelopmentVersionTask = tasks.register( 'changeToDevelopmentVersion' ) {
	group 'Release'
	description 'Updates `version.txt` file to the specified development-version'

	dependsOn tagReleaseTask

	doFirst {
		logger.lifecycle( "Updating version-file to development-version : `${developmentVersion}`" )
		updateVersionFile( developmentVersion )
	}

	doLast {
		logger.lifecycle( "Committing changes to `version.txt` : `${developmentVersion}`" )
		executeGitCommand( 'add', '.' )
		executeGitCommand( 'commit', '-m', "Post-steps for release : `${releaseVersion}`" )
	}
}

def pushToGitTask = tasks.register( 'pushToGit' ) {
	dependsOn changeToDevelopmentVersionTask

	def gitRemote = determineGitRemote( project )
	def gitBranch = determineGitBranch( project )

	doLast {
		logger.lifecycle( "Pushing branch and tag to remote `${gitRemote}`..." )
		executeGitCommand( 'push', '--atomic', gitRemote, gitBranch, releaseVersion )
	}
}

tasks.register( "completeRelease" ) {
	dependsOn tagReleaseTask
	dependsOn changeToDevelopmentVersionTask
	dependsOn pushToGitTask
}


void updateVersionFile(String version) {
	logger.lifecycle( "Updating `version.txt` version to `${version}`" )
	project.ext.versionFile.text = "${version}"
}

static String executeGitCommand(Object ... subcommand){
	List<Object> command = ['git']
	Collections.addAll( command, subcommand )
	def proc = command.execute()
	def code = proc.waitFor()
	def stdout = inputStreamToString( proc.getInputStream() )
	def stderr = inputStreamToString( proc.getErrorStream() )
	if ( code != 0 ) {
		throw new GradleException( "An error occurred while executing " + command + "\n\nstdout:\n" + stdout + "\n\nstderr:\n" + stderr )
	}
	return stdout
}

static String determineGitRemote(Project project) {
	if (project.hasProperty('gitRemote')) {
		return project.property('gitRemote')
	}
	else {
		final String remotes = executeGitCommand( 'remote', 'show' ).trim()
		final List<String> tokens = remotes.tokenize()
		if ( tokens.size() != 1 ) {
			throw new GradleException( "Could not determine `gitRemote` property for `releaseChecks` tasks." )
		}
		return tokens.get( 0 )
	}
}

static String determineGitBranch(Project project) {
	if (project.hasProperty('gitBranch')) {
		return project.property('gitBranch')
	}
	else {
		return executeGitCommand( 'branch', '--show-current' ).trim()
	}
}

static String inputStreamToString(InputStream inputStream) {
	inputStream.withCloseable { ins ->
		new BufferedInputStream(ins).withCloseable { bis ->
			new ByteArrayOutputStream().withCloseable { buf ->
				int result = bis.read()
				while (result != -1) {
					buf.write((byte) result)
					result = bis.read()
				}
				return buf.toString( StandardCharsets.UTF_8.name() )
			}
		}
	}
}