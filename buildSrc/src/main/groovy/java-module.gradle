plugins {
	id "base-information"
	id "java-library"
}

dependencies {
	implementation libs.logging

	compileOnly libs.loggingAnnotations

	annotationProcessor libs.loggingProcessor
	annotationProcessor libs.logging
	annotationProcessor libs.loggingAnnotations
}


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Java handling
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

java {
	sourceCompatibility = jdks.versions.baseline.get() as int
	targetCompatibility = jdks.versions.baseline.get() as int
}

// create a single "compile" task
tasks.register( "compile" ).configure {
	dependsOn tasks.withType( JavaCompile )
	dependsOn tasks.withType( ProcessResources )
}

tasks.withType( JavaCompile ).configureEach {javaCompile->
	options.encoding = "UTF-8"
	options.warnings false
}

// To force the build produce the same byte-for-byte archives and hence make Hibernate Models build reproducible.
// See also https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives
tasks.withType( AbstractArchiveTask ).configureEach {
	preserveFileTimestamps = false
	reproducibleFileOrder = true
}

