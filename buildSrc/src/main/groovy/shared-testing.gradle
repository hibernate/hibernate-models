// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Sets up shared testing
// 		to execute tests from hibernate-models in the context of other modules.
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

plugins {
	id "testing"
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Wire in the shared-tests
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

configurations {
	sharedTestClasses {
		canBeConsumed = false
		canBeResolved = true
	}
	sharedTestResources {
		canBeConsumed = false
		canBeResolved = true
	}
	sharedTestRuntimeClasspath {
		canBeConsumed = false
		canBeResolved = true
	}
}

dependencies {
	testImplementation project( ":hibernate-models-testing" )

	sharedTestClasses project(path: ':hibernate-models', configuration: 'exportedTestClasses')
	sharedTestResources project(path: ':hibernate-models', configuration: 'exportedTestResources')

	sharedTestRuntimeClasspath project(path: ':hibernate-models', configuration: 'exportedTestRuntimeClasspath')
}

tasks.named( "test", Test ) {
	testClassesDirs += configurations.sharedTestClasses
	classpath += configurations.sharedTestResources
	classpath += configurations.sharedTestRuntimeClasspath
}
